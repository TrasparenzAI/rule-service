/*
 * Copyright (c) 2025 Consiglio Nazionale delle Ricerche
 *
 * 	This program is free software: you can redistribute it and/or modify
 * 	it under the terms of the GNU Affero General Public License as
 * 	published by the Free Software Foundation, either version 3 of the
 * 	License, or (at your option) any later version.
 *
 * 	This program is distributed in the hope that it will be useful,
 * 	but WITHOUT ANY WARRANTY; without even the implied warranty of
 * 	MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * 	GNU Affero General Public License for more details.
 *
 * 	You should have received a copy of the GNU Affero General Public License
 * 	along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.cloud.tools.jib' version '3.4.4'
}

group = 'it.cnr.anac.transparency'
description = 'Servizio per l\'applicazione delle regole relative al D.Lgs. n. 33-2013 Trasparenza nella PA'

java {
	sourceCompatibility = '21'
	targetCompatibility = '21'
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2024.0.1")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework:spring-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'

	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.5'
	implementation 'org.apache.commons:commons-text:1.13.0'

	implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	def lombokGroup = 'org.projectlombok', lombokVersion = '1.18.30'
	compileOnly group: lombokGroup, name: 'lombok', version: lombokVersion
	annotationProcessor group: lombokGroup, name: 'lombok', version: lombokVersion

	def mapstructGroup = 'org.mapstruct', mapstructVersion = '1.5.5.Final'
	implementation group: mapstructGroup, name: 'mapstruct', version: mapstructVersion
	annotationProcessor group: mapstructGroup, name: 'mapstruct-processor', version: mapstructVersion

	def luceneGroup = 'org.apache.lucene', luceneVersion = '10.1.0'
	implementation group: luceneGroup, name: 'lucene-core', version: luceneVersion
	implementation group: luceneGroup, name: 'lucene-queryparser', version: luceneVersion
	implementation group: luceneGroup, name: 'lucene-suggest', version: luceneVersion
	implementation group: luceneGroup, name: 'lucene-analysis-common', version: luceneVersion

	implementation 'org.jsoup:jsoup:1.19.1'

	implementation 'commons-codec:commons-codec:1.18.0'

}

tasks.named('test') {
	useJUnitPlatform()
}

springBoot {
    buildInfo()
}
jar {
	enabled = false
}
jib {
	from {
		image = "eclipse-temurin:21-jre@sha256:9cc79f292357e78e18a76e09ec4c0f25c1db041748768028af0bef44ead3bb07"
	}
	container {
		entrypoint = ['/bin/sh', '-c', 'exec java $JAVA_OPTS -cp $( cat /app/jib-classpath-file ) $( cat /app/jib-main-class-file )']
	}
}